
Opinion_GAPS_Purifiers_VS_Supporters = {
    trigger = {
		has_valid_civic = Civic_GAPS_Supporters
		FROM = { 
			OR = {
				has_ai_personality = metalhead
				has_valid_civic = "civic_hive_devouring_swarm"
				AND = {
					NOT = { is_same_species = FROM }
					has_valid_civic = civic_fanatic_purifiers
				}
				has_civic = civic_machine_terminator
				has_civic = civic_machine_assimilator
			} 
		}
    }
    opinion = {
        base = 1000
    }
}
Opinion_GAPS_Supporters_Vs_Purifiers = {
    trigger = {
		OR = {
			has_ai_personality = metalhead
			has_valid_civic = "civic_hive_devouring_swarm"
			AND = {
				NOT = { is_same_species = FROM }
				has_valid_civic = civic_fanatic_purifiers
			}
			has_civic = civic_machine_terminator
			has_civic = civic_machine_assimilator
		}
		FROM = { has_valid_civic = Civic_GAPS_Supporters }
    }
    opinion = {
        base = 1000
    }
}
opinion_Supporters_VS_Zealots = {
    trigger = {
		OR = {
			FROM = { has_valid_civic = Civic_GAPS_Supporters }
			has_valid_civic = Civic_GAPS_Supporters
		}
    }
	opinion = {
		modifier = {
			add = 100
			OR = {
				has_ai_personality = evangelising_zealots
				has_ai_personality = awakened_fallen_empire_spiritualist
			}
			OR = {
				has_ethic = "ethic_spiritualist"
				has_ethic = "ethic_fanatic_spiritualist"
			}
			OR = {
				FROM = { has_ethic = "ethic_materialist" }
				FROM = { has_ethic = "ethic_fanatic_materialist" }
			}				
		}
	}
}
opinion_Supporters_VS_hw_vs_pacifist = {
    trigger = {
		OR = {
			FROM = { has_valid_civic = Civic_GAPS_Supporters }
			has_valid_civic = Civic_GAPS_Supporters
		}
    }
	opinion = {
		modifier = {
			add = 100
			has_ai_personality = honorbound_warriors
			FROM = { has_ethic = "ethic_fanatic_pacifist" }
		}
		modifier = {
			add = 50
			has_ai_personality = honorbound_warriors
			FROM = { has_ethic = "ethic_pacifist" }
		}			
	}
}

opinion_Supporters_VS_dc_vs_oligarchic = {
    trigger = {
		OR = {
			FROM = { has_valid_civic = Civic_GAPS_Supporters }
			has_valid_civic = Civic_GAPS_Supporters
		}
    }
	opinion = {
		modifier = {
			add = 50
			has_ai_personality = democratic_crusaders
			OR = {
				FROM = { has_authority = auth_oligarchic }
				FROM = { has_authority = auth_corporate }
			}		
		}	
	}
}

opinion_Supporters_VS_dc_vs_autocracy = {
    trigger = {
		OR = {
			FROM = { has_valid_civic = Civic_GAPS_Supporters }
			has_valid_civic = Civic_GAPS_Supporters
		}
    }
	opinion = {
		modifier = {
			add = 100
			has_ai_personality = democratic_crusaders	
			FROM = { 
				OR = {
					has_authority = auth_imperial
					has_authority = auth_dictatorial
				}
			}
		}
	}
}
# Xenophobe Fallen Empires aren't fans of bordering other empires
opinion_Supporters_VS_encroaching_borders = {
    trigger = {
		OR = {
			FROM = { has_valid_civic = Civic_GAPS_Supporters }
			has_valid_civic = Civic_GAPS_Supporters
		}
    }
	opinion = {
		modifier = {
			add = 200
			has_ai_personality_behaviour = demands_clear_borders
			from = {
				OR = {
					is_country_type = default
					is_country_type = awakened_fallen_empire
				}			
				any_system_within_border = {
					has_hyperlane_to = root
				}					
			}
		}
	}
}

